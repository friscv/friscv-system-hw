/*
(c) FER, HPC Architecture and Application Research Center, All rights reserved

Use under License Agreement ONLY.

IF, PRIOR TO DOWNLOADING, STORING, INSTALLING, ACTIVATING OR USING THE WORK,
(A) YOU DECIDE YOU ARE UNWILLING TO AGREE TO THE TERMS OF THE PROVIDED LICENSE AGREEMENT, or
(B) YOU DID NOT RECEIVE OR OBTAIN THE LICENSE AGREEMENT, YOU HAVE NO RIGHT TO USE THE WORK AND YOU SHOULD PROMPTLY RETURN THE WORK TO FER, DELETE IT, OR DISABLE IT.

https://hpc.fer.hr/en/hpc
licensing.hpc@fer.hr

Version info is listed in friscv_pkg.sv
*/

`include "friscv_pkg.sv"

module friscv_core_complex #(
    parameter int HART_ID = 0
) (
    input  logic       i_clk,
    input  logic       i_rstn,
    output logic       o_end,
    input  logic       i_msip,
    input  logic       i_mtip,
    input  logic       i_meip,
    input  mtime_t     i_mtime,

    output mem_width_e o_mem_size,
    output addr_t      o_mem_addr,
    output data_t      o_mem_wdata,
    input  data_t      i_mem_rdata,
    output rw_cmd_e    o_mem_rw,
    input  logic       i_mem_wait,
    output logic       o_burst_en,
    input  logic       i_beat_valid
);

// ============================================================
// Level 1 bus: instruction and data memory interfaces
// ============================================================

// Instruction L1 bus
addr_t      w_inst_addr;
data_t      w_inst_data;
logic       w_inst_en;
logic       w_inst_wait;
logic       w_stall_if;
inst_t      w_zsbl_data;

// Data L1 bus
addr_t      w_data_addr;
data_t      w_data_wdata;
data_t      w_data_rdata;
logic       w_data_en;
logic       w_data_wr;
mem_width_e w_data_size;
logic       w_data_wait;
amo_op_e    w_amo_op;

// ============================================================
// Protection and Translation signals
// ============================================================

satp_t       w_satp;
logic        w_sum;
logic        w_mxr;
mode_e       w_mode;
logic        w_flush_tlb;
logic [19:0] w_flush_vpn;
logic        w_flush_vpn_en;
logic [8:0]  w_flush_asid;
logic        w_flush_asid_en;

// ============================================================
// Level 2 bus and L1-L2 arbitration
// ============================================================

addr_t      w_l2_req_addr;
mem_width_e w_l2_req_size;
data_t      w_l2_req_wdata;
rw_cmd_e    w_l2_req_rw;
data_t      w_l2_req_rdata;
logic       w_l2_req_wait;
amo_op_e    w_l2_req_amo_op;

addr_t      w_l2_addr;
mem_width_e w_l2_size;
data_t      w_l2_wdata;
rw_cmd_e    w_l2_rw;
data_t      w_l2_backend_rdata;
logic       w_l2_backend_wait;
amo_op_e    w_l2_amo_op;

friscv_l2_if l2_upstream_if();
friscv_l2_if l2_downstream_if();

// AMO unit signals
rw_cmd_e    w_amo_rw;
data_t      w_amo_store_data;
data_t      w_amo_load_data;
logic       w_amo_core_wait;
logic       w_amo_active;
logic       w_amo_start;
logic       w_amo_bootstrap;
logic       r_amo_addr_valid;
addr_t      r_amo_addr;
mem_width_e r_amo_size;

assign w_amo_start = (w_l2_amo_op != AMO_NONE) &&
                     (w_l2_rw != RW_IDLE) &&
                     !r_amo_addr_valid;
assign w_amo_bootstrap = (w_l2_amo_op != AMO_NONE) && !r_amo_addr_valid;

always_ff @(posedge i_clk) begin
    if (!i_rstn) begin
        r_amo_addr_valid <= 1'b0;
        r_amo_addr       <= '0;
        r_amo_size       <= WIDTH_I32;
    end else begin
        // Freeze AMO target address and size for the whole LOAD-STORE sequence
        if (w_amo_start) begin
            r_amo_addr_valid <= 1'b1;
            r_amo_addr       <= w_l2_addr;
            r_amo_size       <= w_l2_size;
        end else if (r_amo_addr_valid && w_amo_rw == RW_IDLE) begin
            r_amo_addr_valid <= 1'b0;
        end
    end
end

assign o_mem_size  = w_amo_active ? (r_amo_addr_valid ? r_amo_size : w_l2_size) : w_l2_size;
assign o_mem_addr  = w_amo_active ? (r_amo_addr_valid ? r_amo_addr : w_l2_addr) : w_l2_addr;
assign o_mem_wdata = w_amo_active ? w_amo_store_data : w_l2_wdata;

// Page fault signals
logic  w_inst_fault, w_load_fault, w_store_fault;
addr_t w_fault_addr;

if (ENABLE_MMU) begin
    friscv_mmu mmu (
        .i_clk           ( i_clk           ),
        .i_rstn          ( i_rstn          ),

        // Instruction Memory Interface
        .i_inst_addr     ( w_inst_addr     ),
        .o_inst_data     ( w_inst_data     ),
        .i_inst_en       ( w_inst_en       ),
        .o_inst_wait     ( w_inst_wait     ),

        // Data Memory Interface
        .i_data_addr     ( w_data_addr     ),
        .i_data_size     ( w_data_size     ),
        .i_data_wdata    ( w_data_wdata    ),
        .o_data_rdata    ( w_data_rdata    ),
        .i_data_en       ( w_data_en       ),
        .i_data_wr       ( w_data_wr       ),
        .o_data_wait     ( w_data_wait     ),
        .i_amo_op        ( w_amo_op        ),

        // External Memory Interface
        .o_mem_size      ( w_l2_req_size   ),
        .o_mem_addr      ( w_l2_req_addr   ),
        .o_mem_wdata     ( w_l2_req_wdata  ),
        .i_mem_rdata     ( w_l2_req_rdata  ),
        .o_mem_rw        ( w_l2_req_rw     ),
        .i_mem_wait      ( w_l2_req_wait   ),
        .o_amo_op        ( w_l2_req_amo_op ),

        // Protection and Translation Control
        .i_satp          ( w_satp          ),
        .i_sum           ( w_sum           ),
        .i_mxr           ( w_mxr           ),
        .i_mode          ( w_mode          ),
        .i_flush_tlb     ( w_flush_tlb     ),
        .i_flush_vpn     ( w_flush_vpn     ),
        .i_flush_vpn_en  ( w_flush_vpn_en  ),
        .i_flush_asid    ( w_flush_asid    ),
        .i_flush_asid_en ( w_flush_asid_en ),

        // Page fault signals
        .o_inst_fault    ( w_inst_fault    ),
        .o_load_fault    ( w_load_fault    ),
        .o_store_fault   ( w_store_fault   ),
        .o_fault_addr    ( w_fault_addr    )
    );
end else begin
    friscv_l1_arbiter l1_arbiter (
        .i_clk        ( i_clk           ),
        .i_rstn       ( i_rstn          ),

        .i_inst_addr  ( w_inst_addr     ),
        .o_inst_data  ( w_inst_data     ),
        .i_inst_en    ( w_inst_en       ),
        .o_inst_wait  ( w_inst_wait     ),

        .i_data_addr  ( w_data_addr     ),
        .i_data_size  ( w_data_size     ),
        .i_data_wdata ( w_data_wdata    ),
        .o_data_rdata ( w_data_rdata    ),
        .i_data_en    ( w_data_en       ),
        .i_data_wr    ( w_data_wr       ),
        .o_data_wait  ( w_data_wait     ),
        .i_amo_op     ( w_amo_op        ),

        .o_mem_addr   ( w_l2_req_addr   ),
        .o_mem_size   ( w_l2_req_size   ),
        .o_mem_wdata  ( w_l2_req_wdata  ),
        .i_mem_rdata  ( w_l2_req_rdata  ),
        .o_mem_rw     ( w_l2_req_rw     ),
        .i_mem_wait   ( w_l2_req_wait   ),
        .o_amo_op     ( w_l2_req_amo_op ),
        .o_grant_inst (                 ),
        .o_grant_start(                 ),
        .o_grant_start_inst(            )
    );

    assign w_inst_fault  = 1'b0;
    assign w_load_fault  = 1'b0;
    assign w_store_fault = 1'b0;
    assign w_fault_addr  = '0;
end

assign l2_upstream_if.valid  = (w_l2_req_rw != RW_IDLE);
assign l2_upstream_if.addr   = w_l2_req_addr;
assign l2_upstream_if.size   = w_l2_req_size;
assign l2_upstream_if.wdata  = w_l2_req_wdata;
assign l2_upstream_if.rw     = w_l2_req_rw;
assign l2_upstream_if.amo_op = w_l2_req_amo_op;

assign w_l2_req_wait  = l2_upstream_if.stall;
assign w_l2_req_rdata = l2_upstream_if.rdata;

// ============================================================
// Level 2 bus buffering
// ============================================================

if (ENABLE_L2_BUFFER) begin
    friscv_l2_buffer l2_buff (
        .i_clk         ( i_clk            ),
        .i_rstn        ( i_rstn           ),
        .if_upstream   ( l2_upstream_if   ),
        .if_downstream ( l2_downstream_if )
    );
end else begin
    assign l2_upstream_if.stall = l2_downstream_if.stall;
    assign l2_upstream_if.rdata = l2_downstream_if.rdata;

    assign l2_downstream_if.valid  = l2_upstream_if.valid;
    assign l2_downstream_if.addr   = l2_upstream_if.addr;
    assign l2_downstream_if.size   = l2_upstream_if.size;
    assign l2_downstream_if.wdata  = l2_upstream_if.wdata;
    assign l2_downstream_if.rw     = l2_upstream_if.rw;
    assign l2_downstream_if.amo_op = l2_upstream_if.amo_op;
end

assign l2_downstream_if.stall = w_l2_backend_wait;
assign l2_downstream_if.rdata = w_l2_backend_rdata;

assign w_l2_addr   = l2_downstream_if.addr;
assign w_l2_size   = l2_downstream_if.size;
assign w_l2_wdata  = l2_downstream_if.wdata;
assign w_l2_rw     = l2_downstream_if.rw;
assign w_l2_amo_op = l2_downstream_if.amo_op;

// ============================================================
// End signal detection on write to END_ADDRESS
// ============================================================

logic r_end_signal;

always_ff @(posedge i_clk) begin
    if (!i_rstn) begin
        r_end_signal <= 1'b0;
    end else if (ENABLE_HW_HALT && w_data_addr == END_ADDRESS && w_data_en && w_data_wr) begin
        r_end_signal <= 1'b1;
    end
end

assign o_end = r_end_signal;
assign w_stall_if = w_inst_wait || r_end_signal;

// ============================================================
// Core instance
// ============================================================

friscv_core #(
    .HART_ID(HART_ID)
) cpu_0 (
    .i_clk            ( i_clk           ),
    .i_rstn           ( i_rstn          ),

    // Interrupt requests
    .i_msip           ( i_msip          ),
    .i_mtip           ( i_mtip          ),
    .i_meip           ( i_meip          ),

    // CLINT time
    .i_mtime          ( i_mtime         ),

    // Page fault signals
    .i_inst_fault     ( w_inst_fault    ),
    .i_load_fault     ( w_load_fault    ),
    .i_store_fault    ( w_store_fault   ),
    .i_fault_addr     ( w_fault_addr    ),

    // Instruction Memory Interface
    .i_mem_addr_out   ( w_inst_addr     ),
    .i_mem_data_in    ( w_inst_data     ),
    .i_mem_en_out     ( w_inst_en       ),
    .i_mem_wait_in    ( w_stall_if      ),

    // Data memory interface
    .d_mem_addr_out   ( w_data_addr     ),
    .d_mem_data_out   ( w_data_wdata    ),
    .d_mem_data_in    ( w_data_rdata    ),
    .d_mem_en_out     ( w_data_en       ),
    .d_mem_wr_out     ( w_data_wr       ),
    .d_mem_size_out   ( w_data_size     ),
    .d_mem_wait_in    ( w_data_wait     ),
    .d_mem_amo_op_out ( w_amo_op        ),

    // Memory management outputs
    .satp_out         ( w_satp          ),
    .sum_out          ( w_sum           ),
    .mxr_out          ( w_mxr           ),
    .mode_out         ( w_mode          ),
    .flush_tlb_out    ( w_flush_tlb     ),
    .flush_vpn_out    ( w_flush_vpn     ),
    .flush_vpn_en_out ( w_flush_vpn_en  ),
    .flush_asid_out   ( w_flush_asid    ),
    .flush_asid_en_out( w_flush_asid_en )
);

// ============================================================
// Atomic memory operations
// ============================================================

if (ENABLE_EXTENSION_A) begin
    friscv_amo_unit amo_unit (
        .i_clk            ( i_clk            ),
        .i_rstn           ( i_rstn           ),
        .i_amo_op         ( r_amo_addr_valid ? w_l2_amo_op : AMO_NONE ),
        .i_rs2_val        ( w_l2_wdata       ),
        .o_core_load_data ( w_amo_load_data  ),
        .o_core_wait      ( w_amo_core_wait  ),
        .i_mem_wait       ( i_mem_wait       ),
        .o_mem_rw         ( w_amo_rw         ),
        .i_mem_load_data  ( i_mem_rdata      ),
        .o_mem_store_data ( w_amo_store_data )
    );
    // Keep AMO path selected across both LOAD and STORE phases
    assign w_amo_active = (w_l2_amo_op != AMO_NONE) || (w_amo_rw != RW_IDLE) || r_amo_addr_valid;
end else begin
    assign w_amo_active     = 1'b0;
    assign w_amo_rw         = RW_IDLE;
    assign w_amo_store_data = '0;
    assign w_amo_load_data  = '0;
    assign w_amo_core_wait  = 1'b0;
    assign w_amo_bootstrap  = 1'b0;
end

// ============================================================
// Zero-stage bootloader
// ============================================================

if (ZSBL_ROM_SIZE_BYTES > 0) begin
    friscv_zsbl_rom zsbl_rom (
        .i_clk  ( i_clk       ),
        .i_addr ( w_l2_addr   ),
        .o_data ( w_zsbl_data )
    );

    logic w_l2_is_rom;
    addr_t r_rom_addr_prev;
    
    // Intercept reads in the ROM address window before they reach AXI
    assign w_l2_is_rom = (w_l2_addr >= RESET_VEC) &&
                         (w_l2_addr < RESET_VEC + ZSBL_ROM_SIZE_BYTES) &&
                         (w_l2_rw == RW_READ);

    // ROM has 1 cycle latency
    // Only update when we detect a new address change
    always_ff @(posedge i_clk) begin
        if (!i_rstn) begin
            r_rom_addr_prev <= '0;
        end else if (w_l2_is_rom && (w_l2_addr != r_rom_addr_prev)) begin
            r_rom_addr_prev <= w_l2_addr;
        end else if (!w_l2_is_rom) begin
            r_rom_addr_prev <= '0;
        end
    end

    assign w_l2_backend_rdata = w_l2_is_rom ? w_zsbl_data :
                                w_amo_active ? w_amo_load_data :
                                i_mem_rdata;

    assign w_l2_backend_wait = w_l2_is_rom ? (w_l2_addr != r_rom_addr_prev) :
                               w_amo_bootstrap ? 1'b1 :
                               w_amo_active ? w_amo_core_wait :
                               i_mem_wait;

    assign o_mem_rw   = w_l2_is_rom ? RW_IDLE :
                        w_amo_bootstrap ? RW_IDLE :
                        w_amo_active ? w_amo_rw :
                        w_l2_rw;

end else begin
    // No ROM, pass through all reads/writes to AXI (or AMO unit)
    assign w_l2_backend_rdata = w_amo_active ? w_amo_load_data : i_mem_rdata;
    assign w_l2_backend_wait  = w_amo_bootstrap ? 1'b1 :
                                w_amo_active ? w_amo_core_wait :
                                i_mem_wait;
    assign o_mem_rw   = w_amo_bootstrap ? RW_IDLE :
                        w_amo_active ? w_amo_rw :
                        w_l2_rw;
end

// TODO replace when cache connected
assign o_burst_en = 1'b0;

endmodule
